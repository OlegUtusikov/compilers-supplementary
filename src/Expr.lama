-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun extractVals(args) {
  map(fun (arg) {
    case arg of
      Val(v) -> v
      | _ -> failure("Error extract val %s\n", arg.string)
    esac
  }, args)
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn(ref, e) -> case evalList(c, {ref, e}) of
      [c@[s, w], {Ref(name), Val(e)}] -> [[s <- [name, e], w], Val(e)]
      | [c@[s, w], {ElemRef(x, i), Val(e)}] -> x[i] := e; [c, Val(e)]
    esac
    | Var(name) -> [c, Val(s.lookup(name))]
    | Ref(name) -> [c, Ref(name)]
    | Const(v) -> [c, Val(v)]
    | Seq(e1, e2) -> case eval(c, e1) of
      [c, _] -> eval(c, e2)
    esac
    | Ignore (e) -> case eval (c, e) of
      [cn, _] -> [cn, Void]
    esac
    | Skip -> [c, Bottom]
    | Binop(op, e1, e2) -> case evalList(c, {e1, e2}) of
      [c, {Val(e1), Val(e2)}] -> [c, Val(evalOp(op, e1, e2))]
    esac
    | If(cond, e1, e2) ->
      case eval(c, cond) of
        [c, Val(0)] -> eval(c, e2)
        | [c, _] -> eval(c, e1)
      esac
    | While(cond, e) ->
      case eval(c, cond) of
        [c, Val(0)] -> [c, Bottom]
        | [c, _] -> eval(eval(c, e)[0], expr)
      esac
    | DoWhile(e, cond) ->
      case eval(eval(c, e)[0], cond) of
        [c, Val(0)] -> [c, Bottom]
        | [c, _] -> eval(c, expr)
      esac
    | Scope (v, sts) ->
      fun addSymbols (s, v) {
        case v of
          {} -> s
          | vf:vrest ->
            case vf of
              Var (vr) -> addNames (addSymbols(s, vrest), vr)
              | Fun (name, args, body) -> addFunction (addSymbols(s, vrest), name, args, body)
            esac
        esac
      }
      var sn = addSymbols (enterScope (s), v);
      case eval([sn, w], sts) of [[s, w], v] ->
        [[leaveScope (s), w], v]
      esac
    | Call(f, context) ->
      case s.lookup(f) of
        Fun(args, body) -> case evalList(c, context) of
          [c@[ns, w], context] ->
            fun addArgs(s, args) {
              case args of
                [name, Val(v)] : tail -> addArgs(s, tail).addName(name, v)
                | {} -> s
              esac
            }

            case body of
              External -> case evalBuiltin(f, extractVals(context), w) of [v, w] -> [[s, w], Val(v)] esac
              | _ -> var nc = [addArgs(enterFunction(s), zip(args, context)), w];
              fun ([[s, w], v]) { [[leaveFunction(ns, s.getGlobal), w], v] }(eval(nc, body))
            esac
        esac
      esac
    | Set(ex, ev) -> eval(c, Assn(Ref(ex), ev))
    | String(x) -> [c, Val(x)]
    | Elem(ex, ei) -> case eval(c, ex) of
      [c, Val(x)] -> case eval(c, ei) of
        [c, Val(i)] -> [c, Val(case x of
          Sexp(_, x) -> x
          | x -> x
        esac[i])]
      esac
    esac
    | ElemRef(ex, ei) -> case eval(c, ex) of
      [c, Val(x)] -> case eval(c, ei) of
        [c, Val(i)] -> [c, ElemRef(x, i)]
      esac
    esac
    | Array(exs) -> case evalList(c, exs) of
      [c, xs] -> [c, Val(listArray(extractVals(xs)))]
    esac
    | Sexp(name, exs) -> case evalList(c, exs) of
      [c, xs] -> [c, Val(Sexp(name, listArray(extractVals(xs))))]
    esac
    | _ -> failure ("eval expr not implemented: %s\n", expr.string)
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
